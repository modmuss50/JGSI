plugins {
    id 'java'
	id 'maven'
	id 'maven-publish'
	id 'io.franzbecker.gradle-lombok' version '2.0'
}

group 'me.modmuss50'
version '0.0.1'
def ENV = System.getenv()
version = version + "." + (ENV.BUILD_NUMBER ?: "local")

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
	maven {
		name "modmuss50"
		url 'https://maven.modmuss50.me'
	}
}

lombok {
	version = "1.18.4"
	sha256 = "39f3922deb679b1852af519eb227157ef2dd0a21eec3542c8ce1b45f2df39742"
}

dependencies {
	compile 'io.javalin:javalin:2.6.0'
	compile 'com.google.code.gson:gson:2.8.5'
	compile 'org.checkerframework:checker-qual:2.6.0'
	runtimeOnly 'org.slf4j:slf4j-simple:1.7.25'
}


publishing {
	publications {
		maven(MavenPublication) {
			groupId 'me.modmuss50'
			artifactId "jgsi"
			from components.java

			//We dont want slf4j-simple to be put in the maven pom, this is up to the dev
			pom.withXml {
				Node pomNode = asNode()
				pomNode.dependencies.'*'.findAll() {
					it.artifactId.text() == 'slf4j-simple'
				}.each() {
					it.parent().remove(it)
				}
			}
		}
	}
	repositories {
		maven {
			url "http://mavenupload.modmuss50.me/"
			if (project.hasProperty('mavenPass')) {
				credentials {
					username 'buildslave'
					password project.getProperty('mavenPass')
				}
			}
		}
	}
}